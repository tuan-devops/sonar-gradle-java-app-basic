plugins {
    id "jacoco"
    id "java"
    id "application"
    id("org.sonarqube") version "5.1.0.4882"
}

description = 'sonar-gradle-java-app-basic'
version = '1.0'

sonar {
    properties {
        property('sonar.projectName', 'sonar-gradle-java-app-basic')
        property('sonar.projectKey', 'sonar-gradle-java-app-basic')
        property('sonar.sources', 'src/main')
        property('sonar.tests', 'src/test')
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenCentral()
}

dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    implementation 'com.google.guava:guava:31.1-jre'

    // Use JUnit test framework
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    implementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    sourceSets.main.java.srcDirs = ['src']
}

application {
    // Define the main class for the application.
    mainClass='com.example.App'
}

test {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jar {
    manifest {
        // Specify the main class that will be used to run the JAR
        attributes(
            'Main-Class': 'com.example.App'
        )
    }
}

distributions {
    main {
        distributionBaseName = 'app'
    }
}

tasks.named('distZip') {
    // Set the destination directory for the ZIP distribution to 'dist'
    destinationDirectory = file('dist')
}